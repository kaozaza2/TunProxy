apply plugin: 'com.android.application'

android {
    namespace "tun.proxy"
    compileSdk 34

    defaultConfig {
        applicationId "tun.proxy"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 100209
        versionName "1.2.9"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17 -fvisibility=hidden "
                abiFilters 'armeabi-v7a','arm64-v8a', 'x86', 'x86_64'
                arguments "-DCMAKE_VERBOSE_MAKEFILE=1 -DANDROID_FUNCTION_LEVEL_LINKING=ON"
            }
        }

        splits {
            abi {
                enable true
                reset()
                include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
                universalApk true
            }
        }
    }
    signingConfigs {
        debug {
            // @See gradle.properties
            storeFile file(productKeyStore)
            keyAlias productKeyAlias
            storePassword productKeyStorePassword
            keyPassword productKeyAliasPassword
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    ndkVersion = '22.1.7171670'
    lint {
        abortOnError false
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            def abiFilters = output.filters.find { it.filterType == "abi" }
            def abi = abiFilters ?: "universal"
            def buildType = variant.buildType.name == "debug" ? "debug" : "release"

            outputFileName = new File("tunproxy-app_${buildType}_${abi}.apk")
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.2.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.navigation:navigation-fragment:2.8.5'
    implementation 'androidx.navigation:navigation-ui:2.8.5'
}
